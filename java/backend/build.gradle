plugins {
    id 'java'
    id 'org.springframework.boot' version '2.4.2'
    id 'com.revolut.jooq-docker' version '0.3.6'
    id 'groovy'
    id 'uk.gov.hmcts.java' version '0.12.3'
}

group = 'uk.gov.hmcts.ccf'
version = '0.0.1-SNAPSHOT'
def springBootVersion = '2.4.2'

repositories {
    jcenter()
    mavenCentral()
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

dependencies {
    implementation platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
    compileOnly 'org.projectlombok:lombok:1.18.16'
    annotationProcessor 'org.projectlombok:lombok:1.18.16'

    testCompileOnly 'org.projectlombok:lombok:1.18.12'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'

    implementation 'org.jooq:jooq:3.14.4'
    // For jooq
    jdbc 'org.postgresql:postgresql:42.2.18'

    compile("org.springframework.boot:spring-boot-starter-jdbc")

    compile("org.springframework.boot:spring-boot-starter-jooq")
    compile("org.springframework.session:spring-session-jdbc")

    implementation 'org.springdoc:springdoc-openapi-ui:1.5.2'
    implementation group: 'com.google.guava', name: 'guava', version: '30.1-jre'

    implementation group: 'org.springframework.security', name: 'spring-security-config'
    implementation "org.springframework.security:spring-security-web"
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-oauth2-client', version: springBootVersion
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation group: 'org.springframework.cloud', name:'spring-cloud-starter-openfeign', version: '3.0.1'
    implementation "io.github.openfeign:feign-httpclient:11.0"
    implementation "io.github.openfeign:feign-jackson:11.0"

    implementation('org.springframework.boot:spring-boot-starter-hateoas')
    implementation 'org.flywaydb:flyway-core'
    implementation 'net.jodah:typetools:0.6.2'
    implementation 'de.cronn:reflection-util:2.8.0'
    implementation group: 'org.apache.commons', name: 'commons-csv', version: '1.8'
    implementation group: 'org.springframework.retry', name: 'spring-retry', version: '1.3.1'
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.12.1'
    implementation("org.springframework.boot:spring-boot-starter-actuator")


//    developmentOnly "org.springframework.boot:spring-boot-devtools:${springBootVersion}"
    runtimeOnly 'org.postgresql:postgresql'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.springframework.security:spring-security-test'
    integrationTestCompile "org.testcontainers:postgresql:1.15.1"
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.18.1'
    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.3-groovy-2.5'
    testCompile group: 'org.spockframework', name: 'spock-spring', version: '1.3-groovy-2.5'
    testImplementation 'org.codehaus.groovy:groovy-all:2.5.8'
}

compileJava {
    options.compilerArgs << "-Werror"
}

configurations.integrationTestImplementation.extendsFrom(configurations.testImplementation)
configurations.integrationTestRuntimeOnly.extendsFrom(configurations.runtimeOnly)

task integrationTest(type: Test) {
    outputs.file('build/open-api.yaml')
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

integrationTest.mustRunAfter test

check {
    dependsOn(tasks.integrationTest)
}

bootJar {
    archiveFileName = 'backend.jar'
}

bootRun {
    args '--generate-data=true', '--big-ids=true', '--database-host=localhost', '--database-port=55432', '--server.port=9998'
    jvmArgs "-noverify" // Improves dev startup time
    if (project.hasProperty('args')) {
        args project.args.split(',')
    }
}

tasks.withType(Checkstyle) {
    exclude '**/org/jooq/**'
}
tasks {
    generateJooqClasses {
        customizeGenerator {
            generate = new org.jooq.meta.jaxb.Generate()
                    .withImmutablePojos(true)
        }
    }
}

