plugins {
    id 'java'
    id 'org.springframework.boot' version '2.3.5.RELEASE'
    id 'com.revolut.jooq-docker' version '0.3.5'
    id 'groovy'
    id 'uk.gov.hmcts.java' version '0.12.1'
}

group = 'uk.gov.hmcts.ccf'
version = '0.0.1-SNAPSHOT'
def springBootVersion = '2.3.5.RELEASE'

repositories {
    jcenter()
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

dependencies {
    implementation platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
    compileOnly 'org.projectlombok:lombok:1.18.16'
    annotationProcessor 'org.projectlombok:lombok:1.18.16'

    testCompileOnly 'org.projectlombok:lombok:1.18.12'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'

    implementation 'org.jooq:jooq:3.14.3'
    // For jooq
    jdbc 'org.postgresql:postgresql:42.2.18'

    compile("org.springframework.boot:spring-boot-starter-jdbc")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")

    implementation group: 'com.google.guava', name: 'guava', version: '30.0-jre'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.security:spring-security-test'
    implementation 'net.jodah:typetools:0.6.2'
    implementation group: 'org.apache.commons', name: 'commons-csv', version: '1.8'

    developmentOnly "org.springframework.boot:spring-boot-devtools:${springBootVersion}"
    runtimeOnly 'org.postgresql:postgresql'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    integrationTestCompile "org.testcontainers:postgresql:1.15.0-rc2"
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.18.0'
    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.3-groovy-2.5'
    testCompile group: 'org.spockframework', name: 'spock-spring', version: '1.3-groovy-2.5'
    testImplementation 'org.codehaus.groovy:groovy-all:2.5.8'
}

compileJava {
    options.compilerArgs << "-Werror"
}

configurations.integrationTestImplementation.extendsFrom(configurations.testImplementation)
configurations.integrationTestRuntimeOnly.extendsFrom(configurations.runtimeOnly)

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

integrationTest.mustRunAfter test

check {
    dependsOn(tasks.integrationTest)
}

bootJar {
    archiveFileName = 'backend.jar'
}

bootRun {
    args '--generate-data=true', '--database-host=localhost'
    if (project.hasProperty('args')) {
        args project.args.split(',')
    }
}

tasks.withType(Checkstyle) {
    exclude '**/org/jooq/**'
}
