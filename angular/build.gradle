import uk.gov.hmcts.AngularExec

plugins {
  id 'org.openapi.generator' version '5.0.0-beta3'
}

task buildTypescriptClient(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
  generatorName = "typescript-angular"
  def producer = tasks.getByPath(':java:integrationTest').outputs
  inputs.files producer
  inputSpec = producer.files[0].getAbsolutePath()
  def outDir = file('src/generated/client-lib')
  outputDir = outDir.path
  outputs.dir outDir
  configOptions = [
    taggedUnions: "true"
  ]
}

task build(dependsOn: buildTypescriptClient)

task test(type: AngularExec) {
  ngCommand 'ng test --watch=false'
  outputs.dir project.file('build/test')
}

task e2e(type: AngularExec) {
  ngCommand 'ng e2e'
  inputs.dir file('e2e')
  outputs.dir file('build/e2e')
}

task lint(type: AngularExec) {
  ngCommand 'ng lint'
  outputs.dir file('build/lint')
}

e2e.shouldRunAfter lint
test.shouldRunAfter lint

task check(dependsOn: ['lint', 'test', 'e2e'])


