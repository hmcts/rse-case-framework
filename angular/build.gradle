import uk.gov.hmcts.AngularExec

plugins {
  id 'org.openapi.generator' version '4.3.1'
}

task buildTypescriptClient(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
  generatorName = "typescript-angular"
  def producer = tasks.getByPath(':java:integrationTest').outputs
  inputs.files producer
  inputSpec = producer.files[0].getAbsolutePath()
  def outDir = file('src/generated/client-lib')
  outputDir = outDir.path
  outputs.dir outDir
}

task build(dependsOn: buildTypescriptClient)

task test(type: AngularExec) {
  ngCommand 'ng test --watch=false'
  outputs.dir project.file('build/test')
}

task e2e(type: AngularExec) {
  ngCommand 'ng e2e'
  inputs.dir file('e2e')
  outputs.dir file('build/e2e')
}

task check(dependsOn: ['test', 'e2e'])


